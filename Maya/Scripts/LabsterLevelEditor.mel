/*
    WALL
*/
proc CreateWallObject(float $y, float $z, float $rot) {
    
    select -r GameObjects:wall_object;
    string $obj[] = `duplicate -rr -un`;
        
    float $wall_size = `getAttr GameObjects:cc_attributes.wallSize`;
        
    scale -r $wall_size $y $z ;
    
    setAttr ($obj[0]+".rotateY") $rot;
    
    $parentObject = "Grp_Wall";
    
    // parent created object inside parent object
    select $obj[0];
    select -add $parentObject;
    parent;
    
    // rename current object to type name
    rename $obj[0] "Wall";

}

proc CreateWallForm() {
    float $y = `floatField -q -v leveled_wallsize_y`;
    float $z = `floatField -q -v leveled_wallsize_z`;
    float $rot = `floatField -q -v leveled_wallsize_rot`;
    
    CreateWallObject($y, $z, $rot);
}

proc PresetWall(float $y, float $z, float $rot) {
    CreateWallObject($y, $z, $rot);
}

/*
    BLOCK
*/
proc CreateBlockObject(int $x, int $y, int $z) {
    
    select -r GameObjects:block_object;
    string $obj[] = `duplicate -rr -un`;
                
    scale -r $x $y $z ;
        
    $parentObject = "Grp_Block";
    
    // parent created object inside parent object
    select $obj[0];
    select -add $parentObject;
    parent;
    
    // rename current object to type name
    rename $obj[0] "Block";

}

proc PresetBlock(int $x, int $y, int $z) {
    CreateBlockObject($x, $y, $z);
}

proc CreateBlockForm() {
    float $x = `floatField -q -v leveled_blocksize_x`;
    float $y = `floatField -q -v leveled_blocksize_y`;
    float $z = `floatField -q -v leveled_blocksize_z`;
    
    CreateBlockObject($x, $y, $z);
}

/*
    DOOR
*/
proc CreateDoor() {
    select -r GameObjects:door_object;
    string $obj[] = `duplicate -rr -un`;
        
    $parentObject = "Grp_Door";
    
    // parent created object inside parent object
    select $obj[0];
    select -add $parentObject;
    parent;
    
    // rename current object to type name
    rename $obj[0] "Door";
}

/*
    ARTIFACT
*/
proc CreateArtifact() {
    select -r GameObjects:artifact_object;
    string $obj[] = `duplicate -rr -un`;
        
    $parentObject = "Grp_Artifact";
    
    // parent created object inside parent object
    select $obj[0];
    select -add $parentObject;
    parent;
    
    // rename current object to type name
   
     rename $obj[0] "Artifact";
}

// HELPER FUNCTION TO ROUND OFF FLOAT VALUES
global proc float roundoff(float $value,int $precision) {
    return (trunc($value*`pow 10 $precision`+0.5)/`pow 10 $precision`);
}

// HELPER FUNCTION TO GET OBJECTS TRANSFORM INFORMATION (TRANSLATE, SCALE, ROTATE)
proc float[] GetObejctTransformInfo(string $obj, string $transform) {
    float $transfX = `getAttr ($obj+"."+$transform+"X")`;
    float $transfY = `getAttr ($obj+"."+$transform+"Y")`;
    float $transfZ = `getAttr ($obj+"."+$transform+"Z")`;
    
    // CLAMP FLOAT DECIMALS TO 3 USING THE ROUND OFF FUNCTION
    float $xyz[] = {roundoff($transfX,3), roundoff($transfY,3), roundoff($transfZ,3)};
    
    return $xyz;
}

/*
    LABSTER EXPORT XML LEVEL
*/
proc LabsterExportXmlLevel() {
    
    string $filePath = `textField -q -tx leveled_levelfile`;
    
    $fileId = `fopen $filePath "w"` ;  
    
    // create level node
    fprint $fileId "<level>\n";
    
    //float $groundScaleX = `getAttr "ground.scaleX"`;
    //float $groundScaleY = `getAttr "ground.scaleY"`;
    
    // create ground
    fprint $fileId ("<ground><scale>80,1,80</scale><position>0,0,0</position></ground>\n");

    // CREATE WALLS
    select Grp_Wall;
    string $wallNodes[] = `ls -sl -tr -dag`;
        
    for($wall in $wallNodes) {
        // IGNORE FIRST
        if($wall == "Grp_Wall") {
            continue;
        }
        
        float $scale[] = GetObejctTransformInfo($wall, "scale");
        float $translate[] = GetObejctTransformInfo($wall, "translate");
        float $rotate[] = GetObejctTransformInfo($wall, "rotate");
        
        fprint $fileId ("<wall>");
        fprint $fileId ("<position>"+$translate[0]+","+$translate[1]+","+$translate[2]+"</position>");
        fprint $fileId ("<rotation>"+$rotate[0]+","+$rotate[1]+","+$rotate[2]+"</rotation>");
        fprint $fileId ("<scale>"+$scale[0]+","+$scale[1]+","+$scale[2]+"</scale>");
        fprint $fileId ("</wall>\n");
    }
    
     // CREATE BLOCKS
    select Grp_Block;
    string $blockNodes[] = `ls -sl -tr -dag`;
        
    for($block in $blockNodes) {
        // IGNORE FIRST
        if($block == "Grp_Block") {
            continue;
        }
        
        float $scale[] = GetObejctTransformInfo($block, "scale");
        float $translate[] = GetObejctTransformInfo($block, "translate");
        float $rotate[] = GetObejctTransformInfo($block, "rotate");
        
        fprint $fileId ("<block>");
        fprint $fileId ("<position>"+$translate[0]+","+$translate[1]+","+$translate[2]+"</position>");
        fprint $fileId ("<rotation>"+$rotate[0]+","+$rotate[1]+","+$rotate[2]+"</rotation>");
        fprint $fileId ("<scale>"+$scale[0]+","+$scale[1]+","+$scale[2]+"</scale>");
        fprint $fileId ("</block>\n");
    }
    
    // CREATE DOORS
    select Grp_Door;
    string $doorNodes[] = `ls -sl -tr -dag`;
        
    for($door in $doorNodes) {
        // IGNORE FIRST
        if($door == "Grp_Door") {
            continue;
        }
        
        float $scale[] = GetObejctTransformInfo($door, "scale");
        float $translate[] = GetObejctTransformInfo($door, "translate");
        float $rotate[] = GetObejctTransformInfo($door, "rotate");
        
        string $doorKey = `getAttr -as ($door+".doorKey")`;
        
        fprint $fileId ("<door>");
        fprint $fileId ("<type>"+$doorKey+"</type>");
        fprint $fileId ("<position>"+$translate[0]+","+$translate[1]+","+$translate[2]+"</position>");
        fprint $fileId ("<rotation>"+$rotate[0]+","+$rotate[1]+","+$rotate[2]+"</rotation>");
        fprint $fileId ("<scale>"+$scale[0]+","+$scale[1]+","+$scale[2]+"</scale>");
        fprint $fileId ("</door>\n");
    }
        
     // CREATE ARTIFACTS
    select Grp_Artifact;
    string $artifactNodes[] = `ls -sl -tr -dag`;
        
    for($artifact in $artifactNodes) {
        // IGNORE FIRST
        if($artifact == "Grp_Artifact") {
            continue;
        }
        
        float $scale[] = GetObejctTransformInfo($artifact, "scale");
        float $translate[] = GetObejctTransformInfo($artifact, "translate");
        float $rotate[] = GetObejctTransformInfo($artifact, "rotate");
        
        string $artifactType = `getAttr -as ($artifact+".artifactType")`;
        
        fprint $fileId ("<artifact>");
        fprint $fileId ("<type>"+$artifactType+"</type>");
        fprint $fileId ("<position>"+$translate[0]+","+$translate[1]+","+$translate[2]+"</position>");
        fprint $fileId ("<rotation>"+$rotate[0]+","+$rotate[1]+","+$rotate[2]+"</rotation>");
        fprint $fileId ("<scale>"+$scale[0]+","+$scale[1]+","+$scale[2]+"</scale>");
        fprint $fileId ("</artifact>\n");
    }
        
  
    // end level node
    fprint $fileId "</level>";
  
    // Close File  
    fclose $fileId ;
}

/*
    LABSTER LEVEL EDITOR
*/
global proc LabsterLevelEditor() {
    string $level_editor_win = "LEVEL_EDITOR_WIN";
    
    if(`window -exists $level_editor_win`) {
        deleteUI -window $level_editor_win;
    }
    
    window -title "Labster Game Level Editor"
            -width 500
            -height 375
            $level_editor_win;
            
            columnLayout -rowSpacing 12;
                rowLayout -nc 6 -cw6 120 70 70 60 70 30;
                    text -label "Create Wall Y/Z";
                    floatField -value 1 -precision 0 leveled_wallsize_y;
                    floatField -precision 0 leveled_wallsize_z;
                    text -label "rotation";
                    floatField -precision 0 leveled_wallsize_rot;
                    button -label "Create" -c "CreateWallForm";
                setParent ..;
                
                
                rowLayout -nc 4 -cw4 120 80 80 80 ;
                    text -label "Wall Presets";
                    button -label "10x Wall" -c "PresetWall(1, 10, 0)";
                    button -label "4x Wall" -c "PresetWall(1, 4, 0)";
                    button -label "1x Wall" -c "PresetWall(1, 1, 0)";
                setParent ..;
                
                 rowLayout -nc 5 -cw5 120 70 70 60 70;
                    text -label "Create Block X/Y/Z";
                    floatField -precision 0 leveled_blocksize_x;
                    floatField -precision 0 leveled_blocksize_y;
                    floatField -precision 0 leveled_blocksize_z;
                    button -label "Create" -c "CreateBlockForm";
                setParent ..;
                
                rowLayout -nc 4 -cw4 120 80 80 80 ;
                    text -label "Wall Presets";
                    button -label "1x1 Block" -c "PresetBlock(1, 1, 1)";
                    button -label "1x2 Block" -c "PresetBlock(1, 2, 1)";
                    button -label "2x2 Block" -c "PresetBlock(2, 2, 2)";
                setParent ..;
                
                rowLayout -nc 2 -cw2 90 90;
                    button -label "Create Door" -c "CreateDoor";
                    button -label "Create Artifact" -c "CreateArtifact";
                setParent ..;                
                
                rowLayout -nc 2 -cw2 80 220;
                    text -label "Level file";
                    textField -text "/Users/joe/Desktop/works/Labster/GamePrototype/LabsterDemoApplication/labsterdemo/Unity/LabsterDemo/Assets/Levels/Easy.xml" leveled_levelfile;
                    
                setParent ..;
                
                button -label "Export level" -c "LabsterExportXmlLevel";
            
    
    showWindow;
}
